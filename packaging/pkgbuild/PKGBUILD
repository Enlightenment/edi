# Maintainer: Enlightenment Developers <enlightenment-devel@enlightenment.org>
pkgname='edi-git'
pkgver=0.7.0.0001
pkgrel=1
pkgdesc="Efl based IDE - GIT development snapshot"
arch=('i686' 'x86_64' 'arm')
url="https://www.enlightenment.org/about-edi"
license=('GPLv2')
makedepends=('git' 'meson')
depends=('efl' 'clang' 'check')
options=('!libtool' 'debug' '!strip')
provides=('edi')
source=("edi-git::git+https://github.com/Enlightenment/edi.git#branch=develop")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  (set -o pipefail
   git describe --tags 2>/dev/null | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
   printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}
build() {
  cd "$pkgname"
  meson --buildtype=release --prefix=/usr build/
  ninja -C build/
}

check() {
  cd "$pkgname"
  ninja -C build/ test
}

package() {
  cd "$pkgname"
  DESTDIR="$pkgdir/" ninja -C build/ install
}

